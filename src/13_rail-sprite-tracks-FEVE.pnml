// Spritesets para as li√±as do FEVE

#define MAIN_FILE			"src/gfx/FEVE/main.png"
#define DEPOT_FILE			"src/gfx/FEVE/depot.png"
#define FENCES_FILE			"src/gfx/FEVE/fences.png"
#define CROSSINGS_FILE		"src/gfx/FEVE/crossings.png"

spriteset(feve_gui, MAIN_FILE){  rail_gui_template()	}
spriteset(feve_track_overlay, MAIN_FILE) {  rail_track_overlay_template()	}
spriteset(feve_underlay, MAIN_FILE) {  rail_underlay_template()	}
spriteset(feve_tunnels, MAIN_FILE) {  rail_tunnels_template()	}
spriteset(feve_bridge_surfaces, MAIN_FILE) {  rail_bridge_surfaces_template()	}

spriteset(feve_depot, DEPOT_FILE){  rail_depot_template(0)	}
spriteset(feve_depot_snow, DEPOT_FILE){  rail_depot_template(86)	}
// Depot Switch
switch(FEAT_RAILTYPES, SELF, switch_feve_depots,  terrain_type){
	TILETYPE_SNOW: feve_depot_snow;
	feve_depot;
}

spriteset(feve_fences, FENCES_FILE)	{	rail_fences_template()	}

spriteset(feve_crossings_empty,	CROSSINGS_FILE)	{	rail_crossings_empty_template()	}
spriteset(feve_crossings_closed, CROSSINGS_FILE)	{	rail_crossings_closed_template()	}
spriteset(feve_crossings_opened, CROSSINGS_FILE)	{	rail_crossings_opened_template()	}

// Level crossing switches

switch(FEAT_RAILTYPES, SELF, switch_feve_crossings, level_crossing_status){
	LEVEL_CROSSING_CLOSED : feve_crossings_closed ;
	feve_crossings_opened ;
}
#undef MAIN_FILE
#undef DEPOT_FILE
#undef FENCES_FILE
#undef CROSSINGS_FILE

item (FEAT_RAILTYPES, FEVE)
{
	if (param_replace_monorail == 1) {
		property {label:"MONO";}
	}else{
		property {label:"FEVE";}
	}
	property
	{
		introduction_date: 				date(1951, 1, 1);
		name: 							string(STR_FEVE_NAME);
		toolbar_caption: 				string(STR_FEVE_TOOLBAR);
		menu_text: 						string(STR_FEVE_MENU);
		build_window_caption: 			string(STR_FEVE_BUILD_CAPTION);
		autoreplace_text: 				string(STR_FEVE_AUTOREPLACE);
		new_engine_text: 				string(STR_FEVE_NEW_ENGINE);
		compatible_railtype_list: 		["MONO","FEVE"];
		powered_railtype_list: 			["MONO","FEVE"];
		curve_speed_multiplier: 		100;
		station_graphics: 				RAILTYPE_STATION_NORMAL;
		construction_cost: 				FEVE_BASE_COST + FEVE_BASE_COST * param_feve_cost;
		maintenance_cost: 				FEVE_BASE_COST + FEVE_BASE_COST * param_feve_cost;
		speed_limit: 					100 km/h;      
		acceleration_model: 			ACC_MODEL_RAIL;
		map_colour: 					19;
		sort_order: 					10;
		alternative_railtype_list:		["FEVE"];
		railtype_flags:					bitmask(RAILTYPE_FLAG_CATENARY);
    }
    graphics
    {
		gui: 				feve_gui;
		track_overlay: 		feve_track_overlay;
		underlay:			feve_underlay;
		tunnels: 			feve_tunnels;
	//	tunnel_overlay: 	feve_tunnels; 
		bridge_surfaces: 	feve_bridge_surfaces;
		fences:				spriteset_nofences;
		level_crossings: 	switch_feve_crossings;
		depots: 			switch_feve_depots;
	//	signals: 			switch_signals_type;
    }
}
