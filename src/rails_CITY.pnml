/*
#    OpenTTD Galician Railways -  Copyright (C) 2021  Pablo Villaverde Castro
#    This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
#    This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
#	 You should have received a copy of the GNU General Public License along with this program.  If not, see <https://github.com/pvillaverde/openttd-spanish-railsets/blob/main/LICENSE>.
*/
// Spritesets para as liñas do CITY (Ferrocarril de dobre anchura Urbana)
#define GUI_FILE			"src/gfx/CITY/01_gui.gimp.png"
#define OVERLAY_FILE		"src/gfx/CITY/02_overlay.gimp.png"
#define UNDERLAY_FILE		"src/gfx/CITY/03_underlay.gimp.png"
#define BRIDGES_FILE		"src/gfx/CITY/04_bridges.gimp.png"
#define TUNNELS_FILE		"src/gfx/CITY/05_tunnels.gimp.png"
#define DEPOT_FILE			"src/gfx/CITY/06_depot.gimp.png"
#define FENCES_FILE			"src/gfx/CITY/07_fences.gimp.png"
#define CROSSINGS_FILE		"src/gfx/CITY/08_crossings.gimp.png"
#define PRECOMBINED_FILE	"src/gfx/CITY/09_precombined.gimp.png"
#define CATENARY_FILE		"src/gfx/CITY/10_catenary.gimp.png"

// ########################################### GUI: Interface, botóns e cursores
spriteset(city_gui_opengfx_spriteset, GUI_FILE){  rail_gui_gfx_template()	}
spriteset(city_gui_ttd_spriteset, GUI_FILE){  rail_gui_ttd_template()	}
switch (FEAT_RAILTYPES, SELF, city_gui_switch, param_gui) {
    1: city_gui_opengfx_spriteset;
    city_gui_ttd_spriteset;
}
// ########################################### OVERLAY

spriteset(city_overlay_spriteset, OVERLAY_FILE) {  rail_overlay_template(20)	}
spriteset(city_overlay_snow_spriteset, OVERLAY_FILE) {  rail_overlay_template(75)	}
switch(FEAT_RAILTYPES, SELF, city_overlay_switch,  terrain_type){
	TILETYPE_SNOW: city_overlay_snow_spriteset;
	city_overlay_spriteset;
}
// ########################################### UNDERLAY

spriteset(city_underlay_spriteset, UNDERLAY_FILE) {  rail_underlay_template_new(20)	}
spriteset(city_underlay_snow_spriteset, UNDERLAY_FILE) {  rail_underlay_template_new(160)	}
switch(FEAT_RAILTYPES, SELF, city_underlay_switch,  terrain_type){
	TILETYPE_SNOW: city_underlay_snow_spriteset;
	city_underlay_spriteset;
}
// ########################################### BRIDGE SURFACES

spriteset(city_bridges_spriteset, BRIDGES_FILE) {  rail_bridges_template(20)	}
spriteset(city_bridges_snow_spriteset, BRIDGES_FILE) {  rail_bridges_template(75)	}
switch(FEAT_RAILTYPES, SELF, city_bridges_switch,  terrain_type){
	TILETYPE_SNOW: city_bridges_snow_spriteset;
	city_bridges_spriteset;
}
// ########################################### TUNNELS (Tunnel track)
spriteset(city_tunnels_spriteset, TUNNELS_FILE) {  rail_tunnels_template_new(20)	}
spriteset(city_tunnels_snow_spriteset, TUNNELS_FILE) {  rail_tunnels_template_new(90)	}
switch(FEAT_RAILTYPES, SELF, city_tunnels_switch,  terrain_type){
	TILETYPE_SNOW: city_tunnels_snow_spriteset;
	city_tunnels_spriteset;
}
// ########################################### TUNNEL OVERLAY (Tunnel structure)
spriteset(city_tunnels_overlay_spriteset, TUNNELS_FILE) {  rail_tunnels_overlay_template(160)	}
spriteset(city_tunnels_overlay_snow_spriteset, TUNNELS_FILE) {  rail_tunnels_overlay_template(230)	}
switch(FEAT_RAILTYPES, SELF, city_tunnels_overlay_switch,  terrain_type){
	TILETYPE_SNOW: city_tunnels_overlay_snow_spriteset;
	city_tunnels_overlay_spriteset;
}
// ########################################### DEPOTS
spriteset(city_depot, DEPOT_FILE){  rail_depot_template(0)	}
spriteset(city_depot_snow, DEPOT_FILE){  rail_depot_template(86)	}
switch(FEAT_RAILTYPES, SELF, city_depots_switch,  terrain_type){
	TILETYPE_SNOW: city_depot_snow;
	city_depot;
}
// ########################################### FENCES
spriteset(city_fences, FENCES_FILE)	{	rail_fences_new_template()	}
// ########################################### CROSSINGS
// x (NORMAL=0,SNOW=130), y (OLD-CLOSED=60,OLD-OPEN=190,NEW-CLOSED=320,NEW-OPENED=450)
spriteset(city_crossings_closed_old, CROSSINGS_FILE)	{	rail_crossings_template(0,60)	}
spriteset(city_crossings_closed_old_snow, CROSSINGS_FILE)	{	rail_crossings_template(130,60)	}
spriteset(city_crossings_opened_old, CROSSINGS_FILE)	{	rail_crossings_template(0,190)	}
spriteset(city_crossings_opened_old_snow, CROSSINGS_FILE)	{	rail_crossings_template(130,190)	}

spriteset(city_crossings_closed_modern, CROSSINGS_FILE)	{	rail_crossings_template(0,320)	}
spriteset(city_crossings_closed_modern_snow, CROSSINGS_FILE)	{	rail_crossings_template(130,320)	}
spriteset(city_crossings_opened_modern, CROSSINGS_FILE)	{	rail_crossings_template(0,450)	}
spriteset(city_crossings_opened_modern_snow, CROSSINGS_FILE)	{	rail_crossings_template(130,450)	}
// Level Crossing snow switches
switch(FEAT_RAILTYPES, SELF, city_crossings_closed_old_switch,  terrain_type){	TILETYPE_SNOW: city_crossings_closed_old_snow;	city_crossings_closed_old;}
switch(FEAT_RAILTYPES, SELF, city_crossings_opened_old_switch,  terrain_type){	TILETYPE_SNOW: city_crossings_opened_old_snow;	city_crossings_opened_old;}
switch(FEAT_RAILTYPES, SELF, city_crossings_closed_modern_switch,  terrain_type){	TILETYPE_SNOW: city_crossings_closed_modern_snow;	city_crossings_closed_modern;}
switch(FEAT_RAILTYPES, SELF, city_crossings_opened_modern_switch,  terrain_type){	TILETYPE_SNOW: city_crossings_opened_modern_snow;	city_crossings_opened_modern;}
// Level crossing switches

switch(FEAT_RAILTYPES,	SELF,	city_crossings_closed_switch, current_year){ 
	0 .. 1970 - 1	: city_crossings_closed_old_switch ;
	city_crossings_closed_modern_switch;
}

switch(FEAT_RAILTYPES,	SELF,	city_crossings_opened_switch, current_year){ 
	0 .. 1970 - 1	: city_crossings_opened_old_switch ;
	city_crossings_opened_modern_switch;
}

switch(FEAT_RAILTYPES, SELF, city_crossings_switch, level_crossing_status){
	LEVEL_CROSSING_CLOSED : city_crossings_closed_switch ;
	city_crossings_opened_switch ;
}
// ########################################### PRECOMBINED
spriteset(city_precombined, PRECOMBINED_FILE){  rail_precombined_template(20)	}
spriteset(city_precombined_snow, PRECOMBINED_FILE){  rail_precombined_template(360)	}
switch(FEAT_RAILTYPES, SELF, city_precombined_switch,  terrain_type){
	TILETYPE_SNOW: city_precombined_snow;
	city_precombined;
}
// ########################################### CATENARY PYLONS & WIRES

spriteset(city_catenary_pylons, CATENARY_FILE) {	rail_catenary_pylons_template(20)	}
spriteset(city_catenary_pylons_2, CATENARY_FILE) {	rail_catenary_pylons_template(60)	}
random_switch (FEAT_RAILTYPES, SELF, city_catenary_pylons_switch) {    3: city_catenary_pylons;    1: city_catenary_pylons_2;}
spriteset(city_catenary_wires, CATENARY_FILE) {	rail_catenary_wires_template()	}

// ########################################### SIGNALS



#undef GUI_FILE
#undef OVERLAY_FILE
#undef UNDERLAY_FILE
#undef BRIDGES_FILE
#undef TUNNELS_FILE
#undef DEPOT_FILE
#undef FENCES_FILE
#undef CROSSINGS_FILE
#undef PRECOMBINED_FILE
#undef CATENARY_FILE


item (FEAT_RAILTYPES, CITY)
{
	property
	{
		label:							"CITY";
		introduction_date: 				date(1930, 1, 1);
		name: 							string(STR_CITY_NAME);
		toolbar_caption: 				string(STR_CITY_TOOLBAR);
		menu_text: 						string(STR_CITY_MENU);
		build_window_caption: 			string(STR_RAIL_BUILD_CAPTION);
		autoreplace_text: 				string(STR_RAIL_AUTOREPLACE);
		new_engine_text: 				string(STR_RAIL_NEW_ENGINE);
		curve_speed_multiplier: 		100;
		station_graphics: 				RAILTYPE_STATION_NORMAL;
		construction_cost: 				CITY_BASE_COST ^ param_cost_multiplier;
		maintenance_cost: 				CITY_BASE_COST ^ param_cost_multiplier;
		speed_limit: 					250 km/h;      
		sort_order: 					08;
		map_colour: 					08;
		acceleration_model: 			ACC_MODEL_RAIL;
		compatible_railtype_list:   	["ELFE","ELDU","NGRL","NLOW","NAAN","NAAd", "NAAE","ENLW","ELNG","ENHI"];
		powered_railtype_list:      	["ELFE","ELDU","NGRL","NLOW","NAAN","NAAd", "NAAE","ENLW","ELNG","ENHI"];
		alternative_railtype_list:		["ELFE","ELDU","NGRL","NLOW","NAAN","NAAd", "NAAE","ENLW","ELNG","ENHI"];
		railtype_flags:					bitmask(RAILTYPE_FLAG_PRECOMBINED, RAILTYPE_FLAG_CATENARY);
    }
    graphics
    {
		gui: 				city_gui_switch;
		track_overlay: 		city_overlay_switch;
		underlay:			city_underlay_switch;
		bridge_surfaces: 	city_bridges_switch;
		tunnels: 			city_tunnels_switch;
		tunnel_overlay: 	city_tunnels_overlay_switch; 
		depots: 			city_depots_switch;
		fences:				city_fences;
		level_crossings: 	city_crossings_switch;
		precombined:		city_precombined_switch;
		catenary_pylons: 	city_catenary_pylons_switch;
		catenary_wire: 		city_catenary_wires;
	//	signals: 			switch_signals_type;

    }
}
