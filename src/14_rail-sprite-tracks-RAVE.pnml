/*
#    OpenTTD Spanish railsets -  Copyright (C) 2021  Pablo Villaverde Castro
#    This program is free software: you can redistribute it and/or modify it under the terms of the GNU General Public License as published by the Free Software Foundation, either version 3 of the License, or (at your option) any later version.
#    This program is distributed in the hope that it will be useful, but WITHOUT ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the GNU General Public License for more details.
#	 You should have received a copy of the GNU General Public License along with this program.  If not, see <https://github.com/pvillaverde/openttd-spanish-railsets/blob/main/LICENSE>.
*/
// Spritesets para as li침as do RAVE (Red de Alta Velocidad Espa침ola)
#define GUI_FILE			"src/gfx/RAVE/01_gui.png"
#define OVERLAY_FILE		"src/gfx/RAVE/02_overlay.png"
#define UNDERLAY_FILE		"src/gfx/RAVE/03_underlay.png"
#define BRIDGES_FILE		"src/gfx/RAVE/04_bridges.png"
#define TUNNELS_FILE		"src/gfx/RAVE/05_tunnels.png"
#define DEPOT_FILE			"src/gfx/RAVE/06_depot.png"
#define FENCES_FILE			"src/gfx/RAVE/07_fences.png"
#define CROSSINGS_FILE		"src/gfx/RAVE/08_crossings.png"
#define PRECOMBINED_FILE	"src/gfx/RAVE/09_precombined.png"
#define CATENARY_FILE		"src/gfx/RAVE/10_catenary.png"

// ########################################### GUI: Interface, bot칩ns e cursores
spriteset(rave_gui_opengfx_spriteset, GUI_FILE){  rail_gui_gfx_template()	}
spriteset(rave_gui_ttd_spriteset, GUI_FILE){  rail_gui_ttd_template()	}
switch (FEAT_RAILTYPES, SELF, rave_gui_switch, param_gui) {
    1: rave_gui_opengfx_spriteset;
    rave_gui_ttd_spriteset;
}
// ########################################### OVERLAY

spriteset(rave_overlay_spriteset, OVERLAY_FILE) {  rail_overlay_template(20)	}
spriteset(rave_overlay_snow_spriteset, OVERLAY_FILE) {  rail_overlay_template(75)	}
switch(FEAT_RAILTYPES, SELF, rave_overlay_switch,  terrain_type){
	TILETYPE_SNOW: rave_overlay_snow_spriteset;
	rave_overlay_spriteset;
}
// ########################################### UNDERLAY

spriteset(rave_underlay_spriteset, UNDERLAY_FILE) {  rail_underlay_template_new(20)	}
spriteset(rave_underlay_snow_spriteset, UNDERLAY_FILE) {  rail_underlay_template_new(160)	}
switch(FEAT_RAILTYPES, SELF, rave_underlay_switch,  terrain_type){
	TILETYPE_SNOW: rave_underlay_snow_spriteset;
	rave_underlay_spriteset;
}
// ########################################### BRIDGE SURFACES

spriteset(rave_bridges_spriteset, BRIDGES_FILE) {  rail_bridges_template(20)	}
spriteset(rave_bridges_snow_spriteset, BRIDGES_FILE) {  rail_bridges_template(75)	}
switch(FEAT_RAILTYPES, SELF, rave_bridges_switch,  terrain_type){
	TILETYPE_SNOW: rave_bridges_snow_spriteset;
	rave_bridges_spriteset;
}
// ########################################### TUNNELS (Tunnel track)
spriteset(rave_tunnels_spriteset, TUNNELS_FILE) {  rail_tunnels_template_new(20)	}
spriteset(rave_tunnels_snow_spriteset, TUNNELS_FILE) {  rail_tunnels_template_new(90)	}
switch(FEAT_RAILTYPES, SELF, rave_tunnels_switch,  terrain_type){
	TILETYPE_SNOW: rave_tunnels_snow_spriteset;
	rave_tunnels_spriteset;
}
// ########################################### TUNNEL OVERLAY (Tunnel structure)
spriteset(rave_tunnels_overlay_spriteset, TUNNELS_FILE) {  rail_tunnels_overlay_template(160)	}
spriteset(rave_tunnels_overlay_snow_spriteset, TUNNELS_FILE) {  rail_tunnels_overlay_template(230)	}
switch(FEAT_RAILTYPES, SELF, rave_tunnels_overlay_switch,  terrain_type){
	TILETYPE_SNOW: rave_tunnels_overlay_snow_spriteset;
	rave_tunnels_overlay_spriteset;
}
// ########################################### DEPOTS
spriteset(rave_depot, DEPOT_FILE){  rail_depot_template(0)	}
spriteset(rave_depot_snow, DEPOT_FILE){  rail_depot_template(86)	}
switch(FEAT_RAILTYPES, SELF, rave_depots_switch,  terrain_type){
	TILETYPE_SNOW: rave_depot_snow;
	rave_depot;
}
// ########################################### FENCES
spriteset(rave_fences, FENCES_FILE)	{	rail_fences_new_template()	}
// ########################################### CROSSINGS
// x (NORMAL=0,SNOW=130), y (OLD-CLOSED=60,OLD-OPEN=190,NEW-CLOSED=320,NEW-OPENED=450)
spriteset(rave_crossings_closed_old, CROSSINGS_FILE)	{	rail_crossings_template(0,60)	}
spriteset(rave_crossings_closed_old_snow, CROSSINGS_FILE)	{	rail_crossings_template(130,60)	}
spriteset(rave_crossings_opened_old, CROSSINGS_FILE)	{	rail_crossings_template(0,190)	}
spriteset(rave_crossings_opened_old_snow, CROSSINGS_FILE)	{	rail_crossings_template(130,190)	}

spriteset(rave_crossings_closed_modern, CROSSINGS_FILE)	{	rail_crossings_template(0,320)	}
spriteset(rave_crossings_closed_modern_snow, CROSSINGS_FILE)	{	rail_crossings_template(130,320)	}
spriteset(rave_crossings_opened_modern, CROSSINGS_FILE)	{	rail_crossings_template(0,450)	}
spriteset(rave_crossings_opened_modern_snow, CROSSINGS_FILE)	{	rail_crossings_template(130,450)	}
// Level Crossing snow switches
switch(FEAT_RAILTYPES, SELF, rave_crossings_closed_old_switch,  terrain_type){	TILETYPE_SNOW: rave_crossings_closed_old_snow;	rave_crossings_closed_old;}
switch(FEAT_RAILTYPES, SELF, rave_crossings_opened_old_switch,  terrain_type){	TILETYPE_SNOW: rave_crossings_opened_old_snow;	rave_crossings_opened_old;}
switch(FEAT_RAILTYPES, SELF, rave_crossings_closed_modern_switch,  terrain_type){	TILETYPE_SNOW: rave_crossings_closed_modern_snow;	rave_crossings_closed_modern;}
switch(FEAT_RAILTYPES, SELF, rave_crossings_opened_modern_switch,  terrain_type){	TILETYPE_SNOW: rave_crossings_opened_modern_snow;	rave_crossings_opened_modern;}
// Level crossing switches

switch(FEAT_RAILTYPES,	SELF,	rave_crossings_closed_switch, current_year){ 
	0 .. 1970 - 1	: rave_crossings_closed_old_switch ;
	rave_crossings_closed_modern_switch;
}

switch(FEAT_RAILTYPES,	SELF,	rave_crossings_opened_switch, current_year){ 
	0 .. 1970 - 1	: rave_crossings_opened_old_switch ;
	rave_crossings_opened_modern_switch;
}

switch(FEAT_RAILTYPES, SELF, rave_crossings_switch, level_crossing_status){
	LEVEL_CROSSING_CLOSED : rave_crossings_closed_switch ;
	rave_crossings_opened_switch ;
}
// ########################################### PRECOMBINED
spriteset(rave_precombined, PRECOMBINED_FILE){  rail_precombined_template(20)	}
spriteset(rave_precombined_snow, PRECOMBINED_FILE){  rail_precombined_template(360)	}
switch(FEAT_RAILTYPES, SELF, rave_precombined_switch,  terrain_type){
	TILETYPE_SNOW: rave_precombined_snow;
	rave_precombined;
}
// ########################################### CATENARY PYLONS & WIRES

spriteset(rave_catenary_pylons, CATENARY_FILE) {	rail_catenary_pylons_template(20)	}
spriteset(rave_catenary_pylons_2, CATENARY_FILE) {	rail_catenary_pylons_template(60)	}
random_switch (FEAT_RAILTYPES, SELF, rave_catenary_pylons_switch) {    3: rave_catenary_pylons;    1: rave_catenary_pylons_2;}
spriteset(rave_catenary_wires, CATENARY_FILE) {	rail_catenary_wires_template()	}

// ########################################### SIGNALS



#undef GUI_FILE
#undef OVERLAY_FILE
#undef UNDERLAY_FILE
#undef BRIDGES_FILE
#undef TUNNELS_FILE
#undef DEPOT_FILE
#undef FENCES_FILE
#undef CROSSINGS_FILE
#undef PRECOMBINED_FILE
#undef CATENARY_FILE

item (FEAT_RAILTYPES, RAVE)
{
	if (param_replace_maglev == 1) {
		property {label:"MGLV";}
	}else{
		property {label:"RAVE";}
	}
	property
	{
		introduction_date: 				date(1992, 4, 21); // Inauguraci칩n AVE Madrid-Sevilla
		name: 							string(STR_RAVE_NAME);
		toolbar_caption: 				string(STR_RAVE_TOOLBAR);
		menu_text: 						string(STR_RAVE_MENU);
		build_window_caption: 			string(STR_RAVE_BUILD_CAPTION);
		autoreplace_text: 				string(STR_RAVE_AUTOREPLACE);
		new_engine_text: 				string(STR_RAVE_NEW_ENGINE);
		compatible_railtype_list: 		["MGLV","RAVE"];
		powered_railtype_list: 			["MGLV","RAVE"];
		curve_speed_multiplier: 		100;
		station_graphics: 				RAILTYPE_STATION_NORMAL;
		construction_cost: 				RAVE_BASE_COST + RAVE_BASE_COST * param_rave_cost;
		maintenance_cost: 				RAVE_BASE_COST + RAVE_BASE_COST * param_rave_cost;
		speed_limit: 					500 km/h;      
		acceleration_model: 			ACC_MODEL_RAIL;
		sort_order: 					40;
		map_colour: 					40;
		alternative_railtype_list:		["RAVE","MGLV"];
		railtype_flags:					bitmask(RAILTYPE_FLAG_PRECOMBINED, RAILTYPE_FLAG_CATENARY, RAILTYPE_FLAG_NO_LEVEL_CROSSING, RAILTYPE_FLAG_DISALLOW_90DEG);
    }
    graphics
    {
		gui: 				rave_gui_switch;
		track_overlay: 		rave_overlay_switch;
		underlay:			rave_underlay_switch;
		bridge_surfaces: 	rave_bridges_switch;
		tunnels: 			rave_tunnels_switch;
		tunnel_overlay: 	rave_tunnels_overlay_switch; 
		depots: 			rave_depots_switch;
		fences:				rave_fences;
		level_crossings: 	rave_crossings_switch;
		precombined:		rave_precombined_switch;
		catenary_pylons: 	rave_catenary_pylons_switch;
		catenary_wire: 		rave_catenary_wires;
	//	signals: 			switch_signals_custom_type_ELSA;
    }
}
